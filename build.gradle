/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.3/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git-publish:<version>'
    }
}

apply plugin: 'org.ajoberstar.git-publish'


version = '1.4'
sourceCompatibility = 1.7
targetCompatibility = 1.7

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Pipeline Library',  
            'Implementation-Version': version
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
        	groupId 'pipeline'
            artifactId 'pipeline'
            
            from components.java

            artifact sourceJar
            artifact packageJavadoc
        }
    }
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compileClasspath

  options{
    setMemberLevel JavadocMemberLevel.PUBLIC
    setAuthor true

    links "https://docs.oracle.com/javase/7/docs/api/"
  }
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

gitPublish {
    // where to publish to (repo must exist)
    // repoUri = 'git@github.com/ajoberstar/test-repo.git'
    // (or 'https://github.com/ajoberstar/test-repo.git', depending on authentication)

    // branch will be created if it doesn't exist
    branch = 'repository'

    // generally, you don't need to touch this
    // repoDir = file("$buildDir/somewhereelse") // defaults to $buildDir/gitPublish

    // what to publish, this is a standard CopySpec
    contents {
        from 'src/main/java'
        from(javadoc) {
            into 'api'
        }
    }

    // what to keep in the existing branch (include=keep)
    // preserve {
    //     include '1.0.0/**'
    //     exclude '1.0.0/temp.txt'
    // }

    // message used when committing changes
    commitMessage = 'Publishing version ' + version
}


dependencies {
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.1'
    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}


